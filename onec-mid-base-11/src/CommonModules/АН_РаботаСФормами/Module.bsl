#Область ПрограммныйИнтерфейс
// Добавляет на форму Документов элементы "Контактное лицо", "Согласованная скидка"
//
// Параметры:
//   Форма - Форма - Форма документа, на которую добавляются новые элементы.
//
Процедура ДополнитьФорму(Форма) Экспорт
	ИмяФормы = Форма.ИмяФормы;
	
	//Добавление поля "Контактное лицо" в документы
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента"
		ИЛИ ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента"
		ИЛИ ИмяФормы ="Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента"
		ИЛИ ИмяФормы ="Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);
	КонецЕсли;
	
	//Добавление кнопки "Пересчитать" и "Скидка"
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКнопкуПересчитать(Форма);
		ДобавитьПолеСогласованнаяСкидка(Форма);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура  ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("АН_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.АН_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо";
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)
	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("АН_КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.АН_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо";
КонецПроцедуры

Процедура ДобавитьКнопкуПересчитать(Форма)
	КомандаПересчитать = Форма.Команды.Добавить("АН_ПересчитатьСумму");
	КомандаПересчитать.Действие = "АН_ОбработкаНажатияПересчитать";
	КомандаПересчитать.Заголовок = "Пересчитать";
	ЭлементКнопкаПересчитать = Форма.Элементы.Добавить("АНПересчитатьСумму", Тип("КнопкаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ЭлементКнопкаПересчитать.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	ЭлементКнопкаПересчитать.ИмяКоманды = "АН_ПересчитатьСумму";
КонецПроцедуры

Процедура ДобавитьПолеСогласованнаяСкидка(Форма)
	ЭлементСогласованнаяСкидка = Форма.Элементы.Добавить("АНСогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.АН_СогласованнаяСкидка";
	ЭлементСогласованнаяСкидка.Заголовок = "Скидка";
	ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ПриИзмененииАН_СогласованнаяСкидка");

КонецПроцедуры

#КонецОбласти